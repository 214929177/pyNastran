INIT MASTER(S)
ASSIGN OUTPUT2='matrix_single.pch' UNIT=11
$
$ This DMAP alter generates a bunch of matrices for testing
$
SOL 100 $
COMPILE USERDMAP LIST $
ALTER 2 $
$
TYPE PARM,,LOGICAL,Y,,MPATRN=TRUE $
TYPE PARM,,LOGICAL,Y,,MIDENT=TRUE $
TYPE PARM,,LOGICAL,Y,,MRANDM=TRUE $
TYPE PARM,,LOGICAL,Y,,MCMPLX=TRUE $
TYPE PARM,,CHAR8,Y,OFMT=DMIGPCH $ Output format
TYPE PARM,NDDL,I,N,OMAXR $
$
GP1 GEOM1,GEOM2,GEOM3/GPL,EQEXIN,GPDT,CSTM,BGPDT,SIL/S,N,LUSET/
                     S,N,NOCSTM/S,N,NOPOINTS $
$
IF (MPATRN) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' PATTERN MATRIX' $
   $ P1 = 4
   $ P2 = LUSET
   $ P3 = LUSET
   $ P4 = 1 (single precision)
   $MATGEN,/MAT  /4/P2   /P3   /P4/P5/P6/P7/P8/P9 $
   MATGEN ,/PATRN/4/LUSET/LUSET/ 1/  /  1  /1 $
ENDIF $
$
IF (MIDENT) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' IDENTITY MATRIX' $
   MATGEN ,/IDENT/1/LUSET $
ENDIF $
$
IF (MRANDM) THEN $
   MESSAGE //'GENERATING'/LUSET/' X'/LUSET/' RANDOM MATRIX' $
   MATGEN ,/RANDM/5/LUSET/LUSET//1 $
ENDIF $
$
IF (MCMPLX) THEN $
   MESSAGE //'GENERATING'/LUSET/' X          50 COMPLEX MATRIX' $
   MATGEN ,/REAL/5/50/LUSET//2 $
   MATGEN ,/IMAG/5/50/LUSET//3 $
   ADD    REAL,IMAG/CMPLX/(1.0, 0.0)/(0.0, 1.0) $
ENDIF $
$
IF ( OFMT='DMIGPCH' ) THEN $
   MESSAGE //'WRITING IN DMIG FORMAT TO PCH FILE' $
   MATMOD PATRN,EQEXIN/MPATRN,/16/1 $
   MATMOD IDENT,EQEXIN/MIDENT,/16/1 $
   MATMOD RANDM,EQEXIN/MRANDM,/16/1 $
   MATMOD CMPLX,EQEXIN/MCMPLX,/16/1 $
ELSE IF ( OFMT='DMIPCH' ) THEN $
   MESSAGE //'WRITING IN DMI FORMAT TO PCH FILE' $
   MATPCH PATRN,IDENT,RANDM,CMPLX// $
ELSE IF ( OFMT='DMIGOP2' ) THEN $
   MESSAGE //'WRITING IN DMIG FORMAT TO OP2 FILE' $
   MATMOD PATRN,EQEXIN/MPATRN,/16 $
   MATMOD IDENT,EQEXIN/MIDENT,/16 $
   MATMOD RANDM,EQEXIN/MRANDM,/16 $
   MATMOD CMPLX,EQEXIN/MCMPLX,/16 $
   OUTPUT2 MPATRN,MIDENT,MRANDM,MCMPLX//-1/11//OMAXR/'MATPOOL'/
                                  'MATPOOL'/'MATPOOL'/
				  'MATPOOL'/'MATPOOL'//////1 $
ELSE IF ( OFMT='MATOP2' ) THEN $
   MESSAGE //'WRITING IN MATRIX FORMAT TO OP2 FILE' $
   OUTPUT2 PATRN,IDENT,RANDM,CMPLX//-1/11//OMAXR/'MATRIX'/'MATRIX'/
                                  'MATRIX'/'MATRIX'///////1 $
ELSE IF ( OFMT='MATOP4' ) THEN $
   MESSAGE //'WRITING IN MATRIX FORMAT TO OP4 FILE' $
   OUTPUT4 PATRN,IDENT,RANDM,CMPLX//-1/11///16 $
ELSE $
   MESSAGE //'ERROR - UNRECOGNIZED OUTPUT FORMAT' $
ENDIF $
$
CEND
BEGIN BULK
$
$ Choose output format by uncommenting appropriate value
$
$PARAM,OFMT,DMIGPCH $ DMIG OUTPUT FORMAT TO PCH FILE
PARAM,OFMT,DMIPCH  $ DMI OUTPUT FORMAT TO PCH FILE
$PARAM,OFMT,DMIGOP2 $ DMIG OUTPUT FORMAT TO OP2 FILE
$PARAM,OFMT,MATOP2  $ MATRIX OUTPUT2 FORMAT
$PARAM,OFMT,MATOP4  $ MATRIX OUTPUT4 FORMAT (FORMAT ON ASSIGN)
$
$ Uncomment logical=FALSE to turn off specific matrices
$
$PARAM,MPATRN,FALSE
$PARAM,MIDENT,FALSE
$PARAM,MRANDM,FALSE
$PARAM,MCMPLX,FALSE
$
$ Size of matrices is governed by number of DOF, which is
$ controlled by number of SPOINTS
$
SPOINT,1,THRU,43
$
ENDDATA
